import org.owasp.dependencycheck.reporting.ReportGenerator

/*
 * Copyright 2019 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'nu.studer.jooq' version '3.0.3'
    id 'org.owasp.dependencycheck' version '5.3.1'
}

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/release-commons.gradle"
apply from: "$scriptsUrl/signing.gradle"
apply from: "$scriptsUrl/copy-database-scripts.gradle"
apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/jacoco.gradle"
apply from: 'jooq.gradle'

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    if (releaseMode) {
        dependencyRepos.forEach { path ->
            maven {
                setUrl("https://maven.pkg.github.com/reportportal/${path}")
                credentials {
                    username = findProperty("githubUserName")
                    password = findProperty("githubToken")
                }
            }
        }
    } else {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:efc4947e')
        mavenBom('io.zonky.test.postgres:embedded-postgres-binaries-bom:12.0.0')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons'
        compile 'com.epam.reportportal:commons-rules'
        compile 'com.epam.reportportal:commons-model'
    } else {
        compile 'com.github.reportportal:commons:def053af'
        compile 'com.github.reportportal:commons-rules:5.3.0'
        compile 'com.github.reportportal:commons-model:26a6101'
    }

    //https://nvd.nist.gov/vuln/detail/CVE-2020-10683 (dom4j 2.1.3 version dependency) AND https://nvd.nist.gov/vuln/detail/CVE-2019-14900
    compile('org.springframework.boot:spring-boot-starter-data-jpa') {
        exclude group: 'org.hibernate', module: 'hibernate-core'
    }
    compile('org.hibernate:hibernate-core:5.4.18.Final')
    //

    //https://nvd.nist.gov/vuln/detail/CVE-2020-13692
    compile 'org.postgresql:postgresql:42.2.13'
    compile 'org.jooq:jooq'
    jooqRuntime 'org.postgresql:postgresql:42.2.13'

    compile 'io.minio:minio:6.0.13'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compile 'org.hibernate.validator:hibernate-validator'

    compile 'org.apache.commons:commons-collections4:4.4'
    compile 'commons-fileupload:commons-fileupload:1.4'
    compile('commons-validator:commons-validator:1.6') {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }

    compile 'org.jasypt:jasypt:1.9.3'

    compile 'io.zonky.test:embedded-postgres:1.2.6'
    compile 'org.flywaydb:flyway-core:6.3.1'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.flywaydb.flyway-test-extensions:flyway-spring-test:6.1.0'
    
}

dependencyCheck {
    formats = [ReportGenerator.Format.HTML, ReportGenerator.Format.XML]
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["failed"]
        exceptionFormat = "short"
    }
}

checkCommitNeeded.dependsOn removeScripts
test.dependsOn copyTestDatabaseScripts
build.dependsOn jacocoTestReport
