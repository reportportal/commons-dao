import org.owasp.dependencycheck.reporting.ReportGenerator

plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'java-library'
    id 'org.owasp.dependencycheck' version '11.1.0'
    id "org.jooq.jooq-codegen-gradle" version "${jooqVersion}"
}

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/release-commons.gradle"
apply from: "$scriptsUrl/signing.gradle"
apply from: "$scriptsUrl/copy-database-scripts.gradle"
//apply from: "$scriptsUrl/build-quality.gradle"
//apply from: "$scriptsUrl/jacoco.gradle"
apply from: 'jooq.gradle'

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

ext['spring-boot.version'] = '2.5.15'

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:5.13.3' : 'com.epam.reportportal:commons-bom:5.13.3')
        mavenBom('io.zonky.test.postgres:embedded-postgres-binaries-bom:16.2.0')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons'
    } else {
        implementation 'com.github.reportportal:commons:master-SNAPSHOT'
    }

    implementation 'org.springframework.security:spring-security-core'
    implementation 'org.springframework:spring-webmvc'
    implementation 'org.apache.tika:tika-core'

    //https://nvd.nist.gov/vuln/detail/CVE-2020-10683 (dom4j 2.1.3 version dependency) AND https://nvd.nist.gov/vuln/detail/CVE-2019-14900
    api('org.springframework.boot:spring-boot-starter-data-jpa') {
        exclude group: 'org.hibernate', module: 'hibernate-core'
    }
    implementation 'org.springframework:spring-jdbc:5.3.33'
    implementation 'org.hibernate:hibernate-core:5.6.15.Final'
    //

    //https://nvd.nist.gov/vuln/detail/CVE-2020-13692
    implementation 'org.postgresql:postgresql:42.7.3'
    api 'org.jooq:jooq'

    implementation 'io.minio:minio:6.0.13'

    implementation group: 'org.json', name: 'json', version: '20231013'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.hibernate.validator:hibernate-validator'

    api 'org.apache.commons:commons-collections4:4.4'
    api 'commons-fileupload:commons-fileupload:1.4'
    api('commons-validator:commons-validator:1.6') {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }

    api 'org.jasypt:jasypt:1.9.3'

    api 'io.zonky.test:embedded-postgres:2.0.6'
    api 'org.apache.commons:commons-compress:1.26.0'
    implementation 'org.flywaydb:flyway-core:6.3.1'

    api 'org.apache.jclouds.api:s3:2.5.0'
    implementation 'org.apache.jclouds.provider:aws-s3:2.5.0'
    implementation 'org.apache.jclouds.api:filesystem:2.5.0'

    // add lombok support
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    testImplementation 'net.bytebuddy:byte-buddy:1.14.9'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.flywaydb.flyway-test-extensions:flyway-spring-test:6.1.0'
    testImplementation 'org.ow2.asm:asm:9.5'
    testImplementation 'org.ow2.asm:asm-util:9.5'

}

dependencyCheck {
    formats = [ReportGenerator.Format.HTML, ReportGenerator.Format.XML]
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["failed"]
        exceptionFormat = "short"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
}

checkCommitNeeded.dependsOn removeScripts
test.dependsOn copyTestDatabaseScripts
//build.dependsOn jacocoTestReport

tasks.preTagCommit.enabled = false
tasks.updateVersion.enabled = false
tasks.commitNewVersion.enabled = false
