import org.owasp.dependencycheck.reporting.ReportGenerator

plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'java-library'
    id 'nu.studer.jooq' version '3.0.3'
    id 'org.owasp.dependencycheck' version '5.3.1'
}

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/release-commons.gradle"
apply from: "$scriptsUrl/signing.gradle"
apply from: "$scriptsUrl/copy-database-scripts.gradle"
apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/jacoco.gradle"
apply from: 'jooq.gradle'

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:5.11.7' : 'com.epam.reportportal:commons-bom:5.11.7')
        mavenBom('io.zonky.test.postgres:embedded-postgres-binaries-bom:16.2.0')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons'
    } else {
        api 'com.github.reportportal:commons:b7f57fd'
    }

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.9'
    implementation 'org.springframework.security:spring-security-core'
    implementation 'org.springframework:spring-webmvc'
    implementation 'org.apache.tika:tika-core'

    //https://nvd.nist.gov/vuln/detail/CVE-2020-10683 (dom4j 2.1.3 version dependency) AND https://nvd.nist.gov/vuln/detail/CVE-2019-14900
    compile('org.springframework.boot:spring-boot-starter-data-jpa') {
        exclude group: 'org.hibernate', module: 'hibernate-core'
    }
    implementation 'org.springframework:spring-jdbc:5.3.33'
    implementation 'org.hibernate:hibernate-core:5.6.15.Final'
    //

    //https://nvd.nist.gov/vuln/detail/CVE-2020-13692
    compile 'org.postgresql:postgresql:42.7.3'
    compile 'org.jooq:jooq'
    jooqRuntime 'org.postgresql:postgresql:42.7.3'

    compile 'io.minio:minio:6.0.13'

    implementation group: 'org.json', name: 'json', version: '20231013'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compile 'org.hibernate.validator:hibernate-validator'

    compile 'org.apache.commons:commons-collections4:4.4'
    compile 'commons-fileupload:commons-fileupload:1.4'
    compile('commons-validator:commons-validator:1.6') {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }

    compile 'org.jasypt:jasypt:1.9.3'

    compile 'io.zonky.test:embedded-postgres:2.0.6'
    //Fix CVE-2024-25710, CVE-2024-26308
    compile 'org.apache.commons:commons-compress:1.26.0'
    compile 'org.flywaydb:flyway-core:6.3.1'

    compile 'org.apache.jclouds.api:s3:2.5.0'
    compile 'org.apache.jclouds.provider:aws-s3:2.5.0'
    implementation 'org.apache.jclouds.api:filesystem:2.5.0'

    // add lombok support
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.flywaydb.flyway-test-extensions:flyway-spring-test:6.1.0'

}

dependencyCheck {
    formats = [ReportGenerator.Format.HTML, ReportGenerator.Format.XML]
}

test.onlyIf { false }

test {
    useJUnitPlatform()
    testLogging {
        events = ["failed"]
        exceptionFormat = "short"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
}

checkCommitNeeded.dependsOn removeScripts
test.dependsOn copyTestDatabaseScripts
build.dependsOn jacocoTestReport
